buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

plugins {
    id 'idea'
}

project(':money') {
    apply plugin: 'java'
    apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    ext {
        junitJupiterVersion = '5.0.1'
        junitPlatformVersion = '1.0.1'
    }

    repositories {
        jcenter()
    }

    dependencies {
        // JUnit Jupiter API and TestEngine implementation
        testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
        // To avoid compiler warnings about @API annotations in JUnit code
        testCompileOnly 'org.apiguardian:apiguardian-api:1.0.0'
        // Only needed to run tests in an (IntelliJ) IDE(A) that bundles an older version
        testRuntime "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"
    }

    compileTestJava {
        options.compilerArgs += '-parameters'
    }

}
